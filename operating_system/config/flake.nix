{
  description = ''
    NixOS systems' configuration for Raspberry Pi boards
    using nixos-raspberrypi
  '';

  nixConfig = {
    bash-prompt = "\[nixos-raspberrypi\] ➜ ";
    extra-substituters = [
      "https://nixos-raspberrypi.cachix.org"
    ];
    extra-trusted-public-keys = [
      "nixos-raspberrypi.cachix.org-1:4iMO9LXa8BqhU+Rpg6LQKiGa2lsNh/j2oiYLNOQ5sPI="
    ];
    connect-timeout = 5;
  };

  inputs =
  {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-25.11";
    
    nixos-raspberrypi = {
      url = "github:nvmd/nixos-raspberrypi/main";
    };

    disko = {
      url = "github:nix-community/disko";
      inputs.nixpkgs.follows = "nixos-raspberrypi/nixpkgs";
    };

    nixos-anywhere = {
      url = "github:nix-community/nixos-anywhere";
    };
  };

  outputs = { self, nixpkgs
            , nixos-raspberrypi, disko
            , nixos-anywhere, ... }@inputs: let
    allSystems = nixpkgs.lib.systems.flakeExposed;
    forSystems = systems: f: nixpkgs.lib.genAttrs systems (system: f system);       
  in {

    devShells = forSystems allSystems (system: let
      pkgs = nixpkgs.legacyPackages.${system};
    in {
      default = pkgs.mkShell {
        nativeBuildInputs = with pkgs; [
          nil # lsp language server for nix
          nixpkgs-fmt
          nix-output-monitor
          nixos-anywhere.packages.${system}.default
        ];
      };
    });

    nixosConfigurations = let

      required_for_updates  = ({ config, ... }: {
        services.openssh = {
          enable = true;
          settings.PermitRootLogin = "no"; #Switch to using main user. Disable root ssh access.
        };

        #Might be a better altnerative to allow less password typing on updates
        #https://discourse.nixos.org/t/remote-nixos-rebuild-sudo-askpass-problem/28830/15
        #security.pam.sshAgentAuth.enable = true;

        # Don't require sudo/root to `reboot` or `poweroff`.
        security.polkit.enable = true;

        # Allow passwordless sudo from wheel users to avoid prompts during updates.
        security.sudo = {
          enable = true;
          wheelNeedsPassword = false;
        };

        # We are stateless, so just default to latest.
        system.stateVersion = config.system.nixos.release;
      });

      installation_configuration = ({ config, ... }: {
      # This is identical to what nixos installer does in
        # (modulesPash + "profiles/installation-device.nix")

        # Use less privileged nixos user
        users.users.nixos = {
          isNormalUser = true;
          extraGroups = [
            "wheel"
            "networkmanager"
            "video"
          ];
          # Allow the graphical user to login without password
          initialHashedPassword = "";
        };

        # Allow the user to log in as root without a password.
        users.users.root.initialHashedPassword = "";

        # Don't require sudo/root to `reboot` or `poweroff`.
        security.polkit.enable = true;

        # Allow passwordless sudo from nixos user
        security.sudo = {
          enable = true;
          wheelNeedsPassword = false;
        };

        # Automatically log in at the virtual consoles.
        services.getty.autologinUser = "nixos";

        # We run sshd by default. Login is only possible after adding a
        # password via "passwd" or by adding a ssh key to ~/.ssh/authorized_keys.
        # The latter one is particular useful if keys are manually added to
        # installation device for head-less systems i.e. arm boards by manually
        # mounting the storage in a different system.
        services.openssh = {
          enable = true;
          settings.PermitRootLogin = "yes";
        };

        # allow nix-copy to live system
        nix.settings.trusted-users = [ "nixos" ];

        # We are stateless, so just default to latest.
        system.stateVersion = config.system.nixos.release;
      });

      network-config = {
        # This is mostly portions of safe network configuration defaults that
        # nixos-images and srvos provide

        networking.useNetworkd = true;
        # mdns
        #networking.firewall.allowedUDPPorts = [ 5353 ];
        #systemd.network.networks = {
        #  "99-ethernet-default-dhcp".networkConfig.MulticastDNS = "yes";
        #  "99-wireless-client-dhcp".networkConfig.MulticastDNS = "yes";
        #};

        # This comment was lifted from `srvos`
        # Do not take down the network for too long when upgrading,
        # This also prevents failures of services that are restarted instead of stopped.
        # It will use `systemctl restart` rather than stopping it with `systemctl stop`
        # followed by a delayed `systemctl start`.
        systemd.services = {
          systemd-networkd.stopIfChanged = false;
          # Services that are only restarted might be not able to resolve when resolved is stopped before
          systemd-resolved.stopIfChanged = false;
        };

        # Use iwd instead of wpa_supplicant. It has a user friendly CLI
        networking.wireless.enable = false;
        networking.wireless.iwd = {
          enable = true;
          settings = {
            Network = {
              EnableIPv6 = true;
              RoutePriorityOffset = 300;
            };
            Settings.AutoConnect = true;
          };
        };
      };

      common-user-config = {config, pkgs, ... }: {
        imports = [
            #./imports/imports.nix
            required_for_updates #contains config required for updates
            #installation_configuration #From above
            network-config #From above
        ];

        #Discourage swapping on SD card
        boot.kernel.sysctl = { "vm.swappiness" = 10;};

        #time.timeZone = "UTC";
        networking.hostName = "rpi5";

        services.udev.extraRules = ''
          # Ignore partitions with "Required Partition" GPT partition attribute
          # On our RPis this is firmware (/boot/firmware) partition
            ENV{ID_PART_ENTRY_SCHEME}=="gpt", \
            ENV{ID_PART_ENTRY_FLAGS}=="0x1", \
            ENV{UDISKS_IGNORE}="1"
        '';

        environment.systemPackages = with pkgs; [
          tree
        ];


        #users.users.nixos.openssh.authorizedKeys.keys = [
        #  # YOUR SSH PUB KEY HERE #
        #];
        #users.users.root.openssh.authorizedKeys.keys = [
        #  # YOUR SSH PUB KEY HERE #
        #];


        system.nixos.tags = let
          cfg = config.boot.loader.raspberry-pi;
        in [
          "raspberry-pi-${cfg.variant}"
          cfg.bootloader
          config.boot.kernelPackages.kernel.version
        ];
      };

    in {

      rpi5 = nixos-raspberrypi.lib.nixosSystemFull {
        specialArgs = inputs;
        modules = [
          ({ config, pkgs, lib, nixos-raspberrypi, disko, ... }: {
            imports = with nixos-raspberrypi.nixosModules; [
              # Hardware configuration
              raspberry-pi-5.base
              raspberry-pi-5.page-size-16k
              raspberry-pi-5.display-vc4
            ];

            environment.systemPackages = with pkgs; [ 
              ffmpeg #trying to get hardware decoding for h.265
              vdpauinfo
            ];
          })
          
          # Disk configuration
          disko.nixosModules.disko
          ./filesystems.nix { _module.args.file_system_device = "/dev/mmcblk0"; } #The _module.args preface is needed to set the imported config argument

          #Config
          common-user-config
          {
            # Emulated instead of cross compiled to try to use caches
            #Not necessary to set these.
            #nixpkgs.hostPlatform = "aarch64-linux";
            #nixpkgs.buildPlatform = "aarch64-linux";
            
            boot.loader.raspberry-pi.bootloader = "kernel";
            boot.tmp.useTmpfs = true;
          }

          #Cross compile the entire thing. Emulated compilation wasn't working with firefox.
          #cross compile fails with onnxruntime_mlas_test
          #{
          #  nixpkgs.hostPlatform = "aarch64-linux";
          #  nixpkgs.buildPlatform = "x86_64-linux";
          #}

          #This doesn't actually import correctly. It evaluates but isn't part of the final system.
          #({ config, nixpkgs, system, ... }:import ./imports/imports.nix 
          #  {
          #    config=config;
          #    pkgs = import nixpkgs {system="aarch64-linux";}; #use mainline nixpkgs for this
          #  }
          #)

          ({ config, lib, nixpkgs, ... }:{
            imports = [
              ./imports/imports.nix 
            ];
          })
        ];
      };

    };

  };
}